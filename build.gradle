plugins {
    id "com.jfrog.bintray" version "1.2"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7
version = '1.1'
group = 'graphql-java'

repositories {
    mavenCentral()
}

apply plugin: 'groovy'

jar {
    from "LICENSE.md"
}

dependencies {
    compile 'org.antlr:antlr4-runtime:4.5'
    compile 'org.slf4j:slf4j-api:1.7.12'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.4'
    testCompile 'cglib:cglib-nodep:3.1'
    testCompile 'org.objenesis:objenesis:2.1'
}

task sourcesJar(type: Jar) {
    dependsOn classes
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


artifacts {
    archives sourcesJar
    archives javadocJar
}


publishing {
    publications {
        graphqlJava(MavenPublication) {
            version version
            from components.java
            artifact sourcesJar

            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'graphql-java'
                    description 'GraphqL Java'
                    url "https://github.com/andimarek/graphql-java"
                    scm {
                        url "https://github.com/andimarek/graphql-java"
                        connection "https://github.com/andimarek/graphql-java"
                        developerConnection "https://github.com/andimarek/graphql-java"
                    }
                    licenses {
                        license {
                            name 'MIT'
                            url 'https://github.com/andimarek/graphql-java/blob/master/LICENSE.md'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'andimarek'
                            name 'andimarek'
                            email 'andimarek@fastmail.fm'
                        }
                    }
                }
            }
        }
    }
}


bintray {
    user = project["bintray.user"]
    key = project["bintray.key"]
    publications = ['graphqlJava']
    pkg {
        repo = 'graphql-java'
        name = 'graphql-java'
        desc = 'GraphQL in Java'
        licenses = ['MIT']
        vcsUrl= 'https://github.com/andimarek/graphql-java'
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
    distributionUrl = "http://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
